// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
    playServicesVersion = '11.2.0'
    compileSdkVersion = 26
    buildToolsVersion = '26.0.2'
    targetSdkVersion = 26
    minSdkVersion = 19
    OneSignal_AppID = ''
    Google_Project_Number = ''
    CanonicalAppID = 'CanonicalAppID'
    OrganizationID = 'OrganizationID'
    appVersionCode = 1
    appVersionName = '0.1.0'
}

buildscript {
  repositories {
    jcenter()
      google()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.1.1'
    classpath 'com.google.gms:google-services:3.0.0'
  }
}

allprojects {
  repositories {
    maven {
      url "$rootDir/maven"
    }
    maven {
      // We use a modified build of com.android.support.test:runner:1.0.1. Explanation in maven-test/README
      url "$rootDir/maven-test"
    }
    jcenter()
    maven {
      // Local Maven repo containing AARs with JSC built for Android
      url "$rootDir/../js/node_modules/jsc-android/android"
    }
    flatDir {
      dirs 'libs'
      // dirs project(':expoview').file('libs')
    }
    maven { url "https://jitpack.io" }
    maven {
      url 'https://maven.google.com'
    }

    // Want this last so that we never end up with a stale cache
    mavenLocal()
      google()
  }
}

subprojects {
    project.configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force dependencies to use the same version.
                def versionSelector = details.requested
                if (versionSelector.group == 'com.google.firebase'
                        || versionSelector.group == 'com.google.android.gms') {
                    details.useVersion playServicesVersion
                }
            }
        }
    }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
